<html>
<head>
    <title>Lifemapper</title>
    <link rel="preconnect" href="https://fonts.gstatic.com">
    <link href="https://fonts.googleapis.com/css2?family=Open%20Sans&display=swap" rel="stylesheet">
    <style type="text/css">
        canvas {
            padding: 8px;
            border: 1px solid #eee;
            border-radius: 6px;
            box-shadow: 0px 1px 2px #ddd;
        }
        html {
            font-family: "Open Sans", sans-serif;
            font-size: .9em;
            box-sizing: border-box;
        }
        *, *:before, *:after {
          box-sizing: inherit;
        }
        body {
            width: 100%;
            padding: 1em 1.2em;
        }
        h3 {
            margin-bottom: 0;
        }
        p {
            margin-top: .9em;
            width: 800px;
        }
        a {
            color: blue;
            cursor: pointer;
            text-decoration: underline;
        }
        textarea {
            width: 800px;
            height: 300px;
            padding: 14px;
            background-color: #eee;
            border-radius: 6px;
        }
        .subtitle {
            font-size: .8em;
            margin-left: 2em;
            color: #777;
        }
        .downloadlink {
            margin-bottom: .5em;
            display: inline-block;
        }
        #main {
            width: 100%;
            display: flex;
            flex-direction: row;
            flex-wrap: nowrap;
        }
            #canvas {
                margin: 0 1.5em 1.5em 0;
                flex-grow: 0;
                height: fit-content;
            }
            the_big_form {
                min-width: 260px;
                flex-grow: 1;
                overflow: scroll;
            }
                .tabs-wrap {
                    margin-top: 1em;
                }
                    .tabs {
                        border-bottom: 1px solid #999;
                    }
                        .tab {
                            font-size: .9em;
                            margin: 0 .15em -1px;
                            border: 1px solid #999;
                            padding: .4em;
                            display: inline-block;
                        }
                            .tab.active {
                                border-bottom: 1px solid white;
                            }
                    .tabs-content {
                        padding-top: 1em;
                    }
                        .dimmed {
                            opacity: .6;
                        }
                .input_short {
                    width: 70px;
                }
        .footer {
            font-size: .8em;
        }
    </style>
</head>
<body>

    <h3>Lifemapper<span class='subtitle'>don't forget to save your data! (scroll down)</span></h3>
    <hr style='margin-bottom:1em'>

    <download_link></download_link>
    <div id='main'>
        <canvas id="canvas" width="750" height="100">
            <p>Looks like your browser doesn't support the features necessary to run this web app :'(</p>
        </canvas>
        <the_big_form></the_big_form>
    </div>

    <hr>
    <h3>Your lifemap code</h3>
    <p>Copy this code and save it somewhere (e.g. email it to yourself or save it on your computer) &mdash; it contains all the information necessary to recreate your lifemap. Paste it back in this box in the future in order to modify, add to, or re-generate your lifemap.</p>
    <data_codebox></data_codebox>

    <hr>
    <h3>Custom styling</h3>
    <p>The styling of your lifemap is also editable. If you change this from the default, save it as well.</p>
    <styles_codebox></styles_codebox>

    <hr>
    <p class='footer'>Lifemapper is a fully client-side application. Your lifemap data is never transmitted anywhere. If you are concerned about data privacy, you could disconnect your internet before inputting your data. Or, you could download a local copy of the web site (by cloning the <a href='https://github.com/rothos/lifemapper'>git repository</a>) and run it all locally.</p>

    <script src="lodash.js"></script>
    <script src="localDataStorage-1.3.0.trim.js"></script>
    <script src="app.js"></script>
    <script type="coffee">
    # todo:
    # - add edit history + an undo button (accidental deletions are annoying)
    # - optional local storage
    # - add at least a little documentation/notes for the avg user
    #   - not well documented, but very customizable. if ur hack inclined
    #   - can use 'relationships' for 'projects', etc -- anything overlapping
    #   - dates are v flexible, e.g. "2 jan 2001" or "jan 2 2001" or "2001-02-01"
    #
    # wontfix:
    # - sort entries by date/start_date before drawing the lifemap

    init = true
    app_namespace = 'lifemapper_app_s8e2Dj1'
    localData = localDataStorage(app_namespace)

    localStorageDo = (str) ->
        ss = ['data', 'styles', 'ui', 'history']

        if str == 'save'
            stuff = {}
            for s in ss
                localData.set(s, _.cloneDeep(fetch(s)))
        else if str == 'load'
            for s in ss
                if s != 'history'
                    save(_.cloneDeep(localData.get(s)))
                else
                    history = fetch('history')
                    lshist = localData.get(s)
                    history.data_states = _.cloneDeep(lshist.data_states)
                    history.index = lshist.index
        else if str == 'clear'
            for s in ss
                localData.remove(s)

    localStorageCheckpoint = () ->
        ui = fetch('ui')
        if init
            init = false
            if _.get(localData.get('ui'), ['localStorage'])
                console.log('loading...')
                localStorageDo('load')
        else
            if ui.localStorage
                localStorageDo('save')
            else
                localStorageDo('clear')

    historyPush = (data) ->
        maxhistorystates = 200
        history = fetch('history')

        if not history.data_states
            history.data_states = [data]
            history.index = 0
            return

        if not _.isEqual(data, history.data_states[history.index])
            history.data_states = _.slice(history.data_states, 0, history.index+1)
            history.data_states = _.reverse(_.slice(_.reverse(history.data_states), 0, maxhistorystates))
            history.data_states.push(_.cloneDeep(data))
            history.index = history.data_states.length - 1

    historyBack = () ->
        history = fetch('history')
        if history.index > 0
            history.index--
            save(_.clone(history.data_states[history.index])); redraw()

    historyFwd = () ->
        history = fetch('history')
        if history.index < history.data_states.length - 1
            history.index++
            save(_.clone(history.data_states[history.index])); redraw()

    redraw = () ->
        localStorageCheckpoint()
        data = fetch('data')
        styles = fetch('styles')
        clearCanvas()
        drawLifemap(data, styles)

    getRandomString = () -> Math.random().toString(16).substr(2, 8)

    save(_.cloneDeep(empty_data))
    save(_.cloneDeep(default_styles))
    historyPush(_.cloneDeep(fetch('data')))

    # WHY does this fuck up? after the state mutates,
    # the first entry in the array mutates with it..
    #save({data_states: [_.cloneDeep(empty_data)], index: 0, key: "history"})

    dom.EDITABLE_INPUT = (path, label='', classname='') ->
        data = fetch('data')
        str = getRandomString()
        SPAN {},
            LABEL { htmlFor: str }, label
            INPUT
                className: classname
                id: str
                value: _.get(data, path, "")
                onChange: (e) =>
                    _.set(data, path, e.target.value)
                    save(data)
                    historyPush(data)
                    redraw()

    dom.EDITABLE_CHECKBOX = (stateprop, path, label='', defaultval=false, classname='') ->
        obj = fetch(stateprop)
        str = getRandomString()
        SPAN {},
            INPUT
                type: "checkbox"
                checked: _.get(obj, path, defaultval)
                id: str
                onChange: (e) =>
                    _.set(obj, path, !_.get(obj, path, defaultval))
                    save(obj)
                    historyPush(fetch('data'))
                    redraw()
            LABEL { htmlFor: str }, label

    # we're always deleting a specific element from an array
    dom.DELETELINK = (path_to_array, index, text='delete', classname='') ->
        data = fetch('data')
        A
            className: classname
            onClick: (e) =>
                _.get(data, path_to_array).splice(index, 1)
                save(data)
                historyPush(data)
                redraw()
            text

    dom.SORTLINK = (path, sortby, classname='') ->
        data = fetch('data')
        A
            className: classname
            onClick: (e) =>
                _.set(data, path, _.sortBy(_.get(data, path), sortby))
                save(data)
                historyPush(data)
                redraw()
            'sort entries'

    # this is just a simple function. (foreshadowing)
    eventSortFunction = (item) ->
        getDateObj(item.date).getTime()

    dom.EVENTS_TAB_CONTENT = ->
        events = fetch('data').events
        DIV {},
            dom.SORTLINK('events', eventSortFunction)
            BR()
            BR()
            for k in [0..events.length]
                classname = if events[k] then '' else 'dimmed'
                DIV { className: classname },
                    dom.EDITABLE_INPUT(['events', k, 'text'], 'text: ')
                    BR()
                    dom.EDITABLE_INPUT(['events', k, 'date'], 'date: ')
                    if events[k]
                        DIV {},
                            dom.DELETELINK('events', k)
                    BR()

    # note that this is a function that produces an array of functions. whatever
    getRibbonSortFunctionsArray = (item) ->
        fstart = (item) -> getDateObj(item.start_date).getTime()
        fend = (item) -> getDateObj(item.end_date).getTime()
        return [fstart, fend]

    dom.A_RIBBONS_TAB_CONTENT = (category) ->
        items = fetch('data').ribbons[category]
        DIV {},
            dom.SORTLINK(['ribbons', category], getRibbonSortFunctionsArray())
            BR()
            BR()
            for k in [0..items.length]
                classname = if items[k] then '' else 'dimmed'
                DIV { className: classname },
                    dom.EDITABLE_INPUT(['ribbons', category, k, 'text'], 'text: ')
                    BR()
                    dom.EDITABLE_INPUT(['ribbons', category, k, 'start_date'], 'start date: ')
                    BR()
                    dom.EDITABLE_INPUT(['ribbons', category, k, 'end_date'], 'end date: ')
                    if items[k]
                        DIV {},
                            dom.DELETELINK(['ribbons', category], k)
                    BR()

    dom.FORM_TABS = ->
        data = fetch('data')
        ribbon_categories = _.keys(data.ribbons)

        tab_names = _.concat(['events'], ribbon_categories)
        tab_fns = _.concat(
            [dom.EVENTS_TAB_CONTENT],
            _.map(ribbon_categories, (cat) -> (() -> dom.A_RIBBONS_TAB_CONTENT(cat)) )
        )

        ui = fetch('ui')

        if not ui.active_tab
            ui.active_tab = 0
            save(ui)

        DIV { className: 'tabs-wrap' },
            DIV { className: 'tabs' },
                for k in [0..tab_names.length-1]
                    name = tab_names[k]
                    active = if ui.active_tab is k then ' active' else ''

                    A
                        className: 'tab' + active
                        onClick: ((k) ->
                            ui.active_tab = k
                            save(ui)).bind(this, k)
                        name

            DIV { className: 'tabs-content' },
                tab_fns[ui.active_tab]()

    dom.RADIOBUTTONS = (callbacks, labels) ->
        ui = fetch('ui')
        name = getRandomString()
        for k in [0...callbacks.length]
            uniq = getRandomString()
            SPAN {},
                INPUT
                    type: 'radio'
                    id: uniq
                    name: name
                    onClick: callbacks[k]
                    checked: if labels[k] is ui.stylename then true else false
                LABEL { htmlFor: uniq }, labels[k] or 'x'

    setStyle = (name) ->
        ui = fetch('ui')
        ui.stylename = name
        save(ui)
        if name != 'custom'
            styles = fetch('styles')
            styles = all_styles[name]
            save(styles)
        redraw()

    dom.STYLES_FORM = ->
        ui = fetch('ui')

        labels = ['default', 'zebra', 'custom']
        callbacks = _.map( labels, (s) -> (()->setStyle(s)) )

        if not ui.stylename
            ui.stylename = labels[0]
            save(ui)

        DIV {},
            DIV 'styles: '
            dom.RADIOBUTTONS(callbacks, labels)

    dom.THE_BIG_FORM = ->
        checkboxId = getRandomString()
        DIV {},
            DIV {},
                dom.EDITABLE_CHECKBOX('ui', 'localStorage',
                    ' save my data on this computer so i can come back to it later')
                HR()
            DIV {},
                dom.STYLES_FORM()
                HR()
            DIV {},
                A
                    onClick: (e) =>
                        save(demo_data)
                        historyPush(demo_data)
                        redraw()
                    '[try demo data]'
                ' . . . '
                A
                    onClick: (e) =>
                        save(empty_data)
                        historyPush(empty_data)
                        redraw()
                    '[clear all data]'
                HR()
            DIV { style: { textAlign: "right" } },
                A
                    onClick: historyBack
                    '[undo]'
                SPAN(' . . . ')
                A
                    onClick: historyFwd
                    '[redo]'
                BR()
                BR()

            DIV {},
                dom.EDITABLE_INPUT('start_year', 'start year: ', 'input_short')
                BR()
                dom.EDITABLE_INPUT('end_year', 'end year: ', 'input_short')
                BR()
                dom.EDITABLE_INPUT('date_of_birth', 'date of birth: ')
                BR()
                dom.EDITABLE_CHECKBOX('data', 'include_birthdays', 'include birthdays? ', true)
                dom.FORM_TABS()

    download = =>
        link = document.createElement('a')
        link.download = 'lifemap.png'
        link.href = document.getElementById('canvas').toDataURL("image/png")
        link.click()

    dom.DOWNLOAD_LINK = ->
        A
            className: 'downloadlink'
            onClick: download
            'download image (.png)'

    dom.CODEBOX = (stateobj, changeFn=()->null) ->
        TEXTAREA
            value: JSON.stringify(stateobj, null, 4)
            onChange: (e) =>
                changeFn()
                try
                    stateobj = JSON.parse(e.target.value)
                    save(stateobj)
                    if stateobj.key == "data"
                        historyPush(stateobj)
                    redraw()
                catch
                    console.log('warning: failed to parse JSON code, did not redraw lifemap')

    dom.DATA_CODEBOX = ->
        dom.CODEBOX(fetch('data'))

    dom.STYLES_CODEBOX = ->
        changeFn = () ->
            setStyle('custom')
        dom.CODEBOX(fetch('styles'), changeFn)

    # let er rip
    redraw()
    </script>
    <script src="https://stateb.us/client6.js"></script>
</body>
</html>
